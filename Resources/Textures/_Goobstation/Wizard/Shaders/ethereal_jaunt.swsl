// Modified stealth shader

light_mode unshaded;

uniform mediump vec2 reference;
uniform bool use_reference;
uniform lowp float time_scale;
uniform lowp float blink_rate;

const lowp float visibility = 0.1;
const mediump float distance_scale = 0.008;

mediump mat2 rotate(mediump float t)
{
    return mat2(cos(t), -sin(t), sin(t), cos(t));
}

void fragment() {

    highp vec4 sprite = zTexture(UV);

    if (sprite.a == 0.0) {
        discard;
    }

    mediump vec2 center = reference;

    if (use_reference == false) {
        center = 1.0 / SCREEN_PIXEL_SIZE * 0.5;
    }

    highp vec2 coords = (FRAGCOORD.xy + center) * distance_scale;
    coords *= rotate(length(center - FRAGCOORD.xy) * distance_scale);
    highp float w = sin(TIME + (coords.x + coords.y + 2.0*sin(TIME*time_scale) * sin(TIME*time_scale + coords.x - coords.y)));

    w = mix(w, clamp(sin(TIME * blink_rate), -1.0, 0.0), 0.5);
    w = clamp(w, 0.0, 1.0);

    highp vec4 background = vec4(w,w,w,1.0);

    COLOR.xyz = mix(background.xyz, sprite.xyz, visibility);
    COLOR.a = 1.0;
}
